#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, beam, plate, arm, nano, door1, door2, wake1,
       wake2, turret, ground, ignite1, ignite2, ignite3;

static-var  statechg_DesiredState, statechg_StateChanging, notBuilding;

// Signal definitions
#define SIG_MOVE		2
#define SIG_BUILD		4	

SetDirection(heading)
{
	var spd;
	spd = get ABS(heading);
	turn ground to y-axis heading speed spd;
	turn base to y-axis 0-heading speed spd;
}

RockOnWaves()
{
	while(1)
	{
		turn ground to x-axis <1.0> speed <1.5>;
		move ground to y-axis [0.0] speed [0.15];
		wait-for-turn ground around x-axis;
		turn ground to x-axis <-1.0> speed <1.5>;
		move ground to y-axis [-0.1] speed [0.15];
		wait-for-move ground along y-axis;
		move ground to y-axis [0.0] speed [0.15];
		wait-for-turn ground around x-axis;
		turn ground to x-axis <1.0> speed <1.5>;
		move ground to y-axis [-0.1] speed [0.15];
		wait-for-move ground along y-axis;
	}
}

Build()
{
	set-signal-mask SIG_BUILD;
	while( TRUE )
	{
		emit-sfx UNIT_SFX1 from beam;
		sleep 100;
	}
}

activatescr()
{
	turn door1 to z-axis <0.000000> now;
	turn door1 to z-axis <-70.659341> speed <140.000000>;
	turn door2 to z-axis <0.000000> now;
	turn door2 to z-axis <71.560440> speed <140.000000>;
	sleep 130;

	move door1 to y-axis [-3.550000] speed [28.000000];
	move door2 to y-axis [-3.550000] speed [28.000000];
	sleep 130;

	move plate to y-axis [2.300000] speed [8.000000];
	sleep 130;

	turn arm to x-axis <90.0> speed <180.0>;
	turn nano to x-axis <-70.0> speed <140.0>;
	sleep 150;

	set INBUILDSTANCE to 1;
}

deactivatescr()
{
	turn turret to y-axis <0.000000> speed <480.000000>;
	wait-for-turn turret around y-axis;

	turn arm to x-axis <0.000000> speed <180.000000>;
	turn nano to x-axis <0.0> speed <140.000000>;
	sleep 130;

	move plate to y-axis [0.000000] speed [8.000000];
	sleep 130;

	move door1 to y-axis [0.000000] speed [28.000000];
	move door2 to y-axis [0.000000] speed [28.000000];
	sleep 130;

	turn door1 to z-axis <0.000000> speed <540.000000>;
	turn door2 to z-axis <0.000000> speed <540.000000>;
	sleep 150;

	set INBUILDSTANCE to 0;
}

FlameControl(Func_Var_3)
{
	while( TRUE )
	{
		Func_Var_3 = get HEALTH;
		if( Func_Var_3 <= 75 )
		{
			emit-sfx UNIT_SFX2 from ignite1;
			if( Func_Var_3 <= 50 ){
				emit-sfx UNIT_SFX2 from ignite2;
				if( Func_Var_3 <= 25 )	emit-sfx UNIT_SFX2 from ignite3;
			}
			sleep 66;
		}
		else
		{
			sleep 333;
		}
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	start-script FlameControl();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = SFXTYPE_BLACKSMOKE;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = SFXTYPE_WHITESMOKE;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

waveSplash()
{
	while( TRUE )
	{
		turn base to x-axis <-3.0> speed <3.0>;
		move base to y-axis [0.23] speed [0.23];
		wait-for-turn base around x-axis;
		turn base to x-axis <0.0> speed <3.0>;
		move base to y-axis [0.0] speed [0.23];
		wait-for-turn base around x-axis;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	var waketime;
	start-script waveSplash();
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		waketime = (get CURRENT_SPEED) * 100 / get MAX_SPEED;
		if (waketime < 50) waketime = 50;
		sleep 15000/waketime;
	}
}

StopMoving()
{
	signal SIG_MOVE;
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState )
		{
			call-script deactivatescr();
			notBuilding = 1;
			currentstate = 1;
		}
		else
		{
			call-script activatescr();
			notBuilding = 0;
			currentstate = 0;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	hide ignite1;
	hide ignite2;
	hide ignite3;
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
	notBuilding = 1;
	start-script SmokeUnit();
	start-script RockOnWaves();
}

Activate()
{
	start-script RequestState(0);
}

Deactivate()
{
	start-script RequestState(1);
}

StartBuilding(Func_Var_1)
{
	start-script RequestState(0);
	while( notBuilding ) sleep 100;
	signal SIG_BUILD;
	turn turret to y-axis Func_Var_1 speed <480.000000>;
	wait-for-turn turret around y-axis;
	start-script Build();
}

StopBuilding()
{
	signal SIG_BUILD;	
	set-signal-mask SIG_BUILD;
	sleep 6000;
	set-signal-mask 0;
	signal SIG_BUILD;	
	start-script RequestState(1);
}

QueryNanoPiece(piecenum)
{
	piecenum = beam;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode arm type BITMAPONLY | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode beam type BITMAPONLY | BITMAP3;
		explode door1 type BITMAPONLY | BITMAP4;
		explode door2 type BITMAPONLY | BITMAP5;
		explode nano type BITMAPONLY | BITMAP1;
		explode plate type BITMAPONLY | BITMAP2;
		explode turret type BITMAPONLY | BITMAP3;
		explode wake1 type BITMAPONLY | BITMAP4;
		explode wake2 type BITMAPONLY | BITMAP5;
		return (0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode arm type FALL | EXPLODE_ON_HIT | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode beam type FALL | EXPLODE_ON_HIT | BITMAP3;
		explode door1 type SHATTER | EXPLODE_ON_HIT | BITMAP4;
		explode door2 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
		explode nano type FALL | EXPLODE_ON_HIT | BITMAP1;
		explode plate type BITMAPONLY | BITMAP2;
		explode turret type FALL | EXPLODE_ON_HIT | BITMAP3;
		explode wake1 type FALL | EXPLODE_ON_HIT | BITMAP4;
		explode wake2 type FALL | EXPLODE_ON_HIT | BITMAP5;
		return (0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode arm type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode beam type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		explode door1 type SHATTER | EXPLODE_ON_HIT | BITMAP4;
		explode door2 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
		explode nano type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode plate type BITMAPONLY | BITMAP2;
		explode turret type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		explode wake1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
		explode wake2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
		return (0);
	}
	corpsetype = 3;
	explode arm type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode base type BITMAPONLY | BITMAP2;
	explode beam type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode door1 type SHATTER | EXPLODE_ON_HIT | BITMAP4;
	explode door2 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
	explode nano type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode plate type BITMAPONLY | BITMAP2;
	explode turret type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode wake1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
	explode wake2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
}
